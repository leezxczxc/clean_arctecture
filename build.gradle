buildscript {
    ext {
        springBootVersion = '2.3.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:1.0.9.RELEASE"
    }
}

allprojects {
    group 'org.leezxczxc.sample'
    version '1.0-SNAPSHOT'
}
subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 11

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
        implementation 'org.springframework.boot:spring-boot-starter-logging'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
}

project(':application') {
    dependencies {
        implementation project(':usecase')
    }
}

project(':usecase') {
    dependencies {
        implementation project(':domain')
        implementation project(':repository')
    }
}

project(':repository') {
    dependencies {
        implementation project(':domain')
    }
}
